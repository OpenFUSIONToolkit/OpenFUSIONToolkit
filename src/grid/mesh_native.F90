!------------------------------------------------------------------------------
! Flexible Unstructured Simulation Infrastructure with Open Numerics (OpenFUSIONToolkit)
!------------------------------------------------------------------------------
!> @file oft_mesh_native.F90
!
!> Mesh handling for OFT native format, generated by python pre-processing
!! scripts.
!!
!! @author Chris Hansen
!! @date May 2023
!! @ingroup doxy_oft_grid
!------------------------------------------------------------------------------
module oft_mesh_native
USE oft_base
USE oft_io, ONLY: hdf5_field_get_sizes, hdf5_read, hdf5_field_exist
USE oft_mesh_type, ONLY: oft_amesh, oft_mesh, oft_bmesh, mesh, smesh
USE oft_tetmesh_type, ONLY: oft_tetmesh
USE oft_trimesh_type, ONLY: oft_trimesh
USE oft_hexmesh_type, ONLY: oft_hexmesh
USE oft_quadmesh_type, ONLY: oft_quadmesh
USE oft_mesh_local_util, ONLY: mesh_local_findedge, mesh_local_findface
USE oft_mesh_global_util, ONLY: mesh_global_resolution
USE multigrid, ONLY: mg_mesh
IMPLICIT NONE
#include "local.h"
CHARACTER(LEN=80) :: filename = 'none' !< Name of input file for mesh
INTEGER(i4), PARAMETER, PUBLIC :: mesh_native_id = 0
REAL(r8), ALLOCATABLE, PUBLIC :: r_mem(:,:)
INTEGER(i4), ALLOCATABLE, PUBLIC :: lc_mem(:,:)
INTEGER(i4), ALLOCATABLE, PUBLIC :: reg_mem(:)
INTEGER(i4) :: np_ho
REAL(r8), ALLOCATABLE :: r_ho(:,:)
INTEGER(i4), ALLOCATABLE :: le_ho(:,:)
CONTAINS
!------------------------------------------------------------------------------
!> Read in t3d mesh file from file "filename"
!! - Read in T3D options from input file
!! - Read in mesh points and cells
!! - Read in surface IDs for CAD edges and faces
!------------------------------------------------------------------------------
subroutine native_load_mesh
logical :: success
integer(i4) :: i,id,lenreflag,ierr,io_unit,ndims,np_mem,mesh_order
integer(i4), allocatable, dimension(:) :: dim_sizes
!---Read in mesh options
namelist/native_mesh_options/filename!,inpname,reflect,ref_per,zstretch
DEBUG_STACK_PUSH
np_mem=-1
IF(oft_env%head_proc)THEN
    IF(ALLOCATED(r_mem).AND.ALLOCATED(lc_mem).AND.ALLOCATED(reg_mem))THEN
        np_mem=SIZE(r_mem,DIM=2)
    ELSE
        OPEN(NEWUNIT=io_unit,FILE=oft_env%ifile)
        READ(io_unit,native_mesh_options,IOSTAT=ierr)
        CLOSE(io_unit)
        IF(ierr<0)CALL oft_abort('No "native_mesh_options" found in input file.','native_load_vmesh',__FILE__)
        IF(ierr>0)CALL oft_abort('Error parsing "native_mesh_options" in input file.','native_load_vmesh',__FILE__)
        IF(TRIM(filename)=='none')CALL oft_abort('No mesh file specified','native_load_vmesh',__FILE__)
        ! IF(TRIM(inpname)=='none')CALL oft_abort('No T3D input file specified','native_load_vmesh',__FILE__)
        ! lenreflag=lnblnk(reflect)
        !
        WRITE(*,*)
        WRITE(*,'(A)')'**** Loading OFT mesh'
        WRITE(*,'(2X,2A)')'Mesh File = ',TRIM(filename)
        ! WRITE(*,'(2X,2A)')'Geom File = ',TRIM(inpname)
        IF(lenreflag>0)THEN
            ! WRITE(*,'(2X,A)')'Reflection:'
            ! DO i=1,lenreflag
            !     SELECT CASE(reflect(i:i))
            !     CASE('x')
            !         WRITE(*,'(4X,A,L)')'YZ-plane, periodic = ',ref_per(1)
            !     CASE('y')
            !         WRITE(*,'(4X,A,L)')'XZ-plane, periodic = ',ref_per(2)
            !     CASE('z')
            !         WRITE(*,'(4X,A,L)')'XY-plane, periodic = ',ref_per(3)
            !     END SELECT
            ! END DO
        END IF
    END IF
END IF
!---Broadcast input information
#ifdef HAVE_MPI
CALL MPI_Bcast(np_mem,1,OFT_MPI_I4,0,oft_env%COMM,ierr)
IF(ierr/=0)CALL oft_abort('Error in MPI_Bcast','mesh_t3d_load',__FILE__)
CALL MPI_Bcast(filename,80,OFT_MPI_CHAR,0,oft_env%COMM,ierr)
IF(ierr/=0)CALL oft_abort('Error in MPI_Bcast','mesh_t3d_load',__FILE__)
! CALL MPI_Bcast(inpname, 40,OFT_MPI_CHAR,0,oft_env%COMM,ierr)
! IF(ierr/=0)CALL oft_abort('Error in MPI_Bcast','mesh_t3d_load',__FILE__)
! CALL MPI_Bcast(reflect,  3,OFT_MPI_CHAR,0,oft_env%COMM,ierr)
! IF(ierr/=0)CALL oft_abort('Error in MPI_Bcast','mesh_t3d_load',__FILE__)
! CALL MPI_Bcast(ref_per,  3,OFT_MPI_LOGICAL,0,oft_env%COMM,ierr)
! IF(ierr/=0)CALL oft_abort('Error in MPI_Bcast','mesh_t3d_load',__FILE__)
! CALL MPI_Bcast(zstretch, 1,OFT_MPI_R8,0,oft_env%COMM,ierr)
! IF(ierr/=0)CALL oft_abort('Error in MPI_Bcast','mesh_t3d_load',__FILE__)
#endif
!---Read in mesh sizes
IF(np_mem>0)THEN
    ALLOCATE(dim_sizes(2))
    dim_sizes=SHAPE(lc_mem)
ELSE
    CALL hdf5_field_get_sizes(TRIM(filename),"mesh/LC",ndims,dim_sizes)
    IF(ndims==-1)CALL oft_abort('"mesh/LC" field does not exist in input file', 'native_load_vmesh', __FILE__)
END IF
IF(dim_sizes(2)==0)CALL oft_abort('Zero length cell list','native_load_vmesh',__FILE__)
!---Allocate mesh
SELECT CASE(dim_sizes(1))
    CASE(4)
        ALLOCATE(oft_tetmesh::mg_mesh%meshes(mg_mesh%mgdim))
        ALLOCATE(oft_trimesh::mg_mesh%smeshes(mg_mesh%mgdim))
    CASE(8)
        ALLOCATE(oft_hexmesh::mg_mesh%meshes(mg_mesh%mgdim))
        ALLOCATE(oft_quadmesh::mg_mesh%smeshes(mg_mesh%mgdim))
    CASE DEFAULT
        CALL oft_abort('Invalid cell type','native_load_vmesh',__FILE__)
END SELECT
DO i=1,mg_mesh%mgdim
    CALL mg_mesh%meshes(i)%setup(mesh_native_id)
    CALL mg_mesh%smeshes(i)%setup(mesh_native_id,.TRUE.)
    mg_mesh%meshes(i)%bmesh=>mg_mesh%smeshes(i)
END DO
mesh=>mg_mesh%meshes(1)
mesh%nc=dim_sizes(2)
smesh=>mg_mesh%smeshes(1)
!
IF(np_mem>0)THEN
    ALLOCATE(dim_sizes(2))
    dim_sizes=SHAPE(r_mem)
ELSE
    CALL hdf5_field_get_sizes(TRIM(filename),"mesh/R",ndims,dim_sizes)
    IF(ndims==-1)CALL oft_abort('"mesh/R" field does not exist in input file', 'native_load_vmesh', __FILE__)
END IF
mesh%np=dim_sizes(2)
IF(mesh%np==0)CALL oft_abort('Zero length point list','native_load_vmesh',__FILE__)
IF(.NOT.oft_env%head_proc)THEN
    DEBUG_STACK_POP
    RETURN
END IF
!---Read in points
ALLOCATE(mesh%r(3,mesh%np))
IF(np_mem>0)THEN
    mesh%r=r_mem
    DEALLOCATE(r_mem)
ELSE
    CALL hdf5_read(mesh%r,TRIM(filename),"mesh/R",success)
    IF(.NOT.success)CALL oft_abort('Error reading points','native_load_vmesh',__FILE__)
END IF
!---Read in cells
ALLOCATE(mesh%lc(mesh%cell_np,mesh%nc))
IF(np_mem>0)THEN
    mesh%lc=lc_mem
    DEALLOCATE(lc_mem)
ELSE
    CALL hdf5_read(mesh%lc,TRIM(filename),"mesh/LC",success)
    IF(.NOT.success)CALL oft_abort('Error reading cells','native_load_vmesh',__FILE__)
END IF
!---Read in region list
ALLOCATE(mesh%reg(mesh%nc))
IF(np_mem>0)THEN
    mesh%reg=reg_mem
    DEALLOCATE(reg_mem)
ELSE
    CALL hdf5_read(mesh%reg,TRIM(filename),"mesh/REG",success)
    IF(.NOT.success)CALL oft_abort('Error reading region list','native_load_vmesh',__FILE__)
END IF
!---Read high-order mesh information (Tetrahedra only)
IF(hdf5_field_exist(TRIM(filename),"mesh/ho_info/LE"))THEN
    mesh_order=2
    CALL hdf5_field_get_sizes(TRIM(filename),"mesh/ho_info/R",ndims,dim_sizes)
    IF(ndims==-1)CALL oft_abort('"mesh/ho_info/R" field does not exist in input file', 'native_load_vmesh', __FILE__)
    np_ho=dim_sizes(2)
    ALLOCATE(r_ho(3,np_ho))
    CALL hdf5_read(r_ho,TRIM(filename),"mesh/ho_info/R",success)
    IF(.NOT.success)CALL oft_abort('Error reading quadratic points','native_load_vmesh',__FILE__)
    ALLOCATE(le_ho(2,np_ho))
    CALL hdf5_read(le_ho,TRIM(filename),"mesh/ho_info/LE",success)
    IF(.NOT.success)CALL oft_abort('Error reading quadratic edges','native_load_vmesh',__FILE__)
ELSE
    mesh_order=1
END IF
IF(oft_debug_print(2))WRITE(*,*)'  Complete'
!---
! call mesh_t3d_geom
call mesh_global_resolution(smesh)
! !---
! do i=1,lenreflag
!     if(reflect(i:i)=='x')call mesh_t3d_reflect(1,.1d0*mesh%hmin,ref_per(i))
!     if(reflect(i:i)=='y')call mesh_t3d_reflect(2,.1d0*mesh%hmin,ref_per(i))
!     if(reflect(i:i)=='z')call mesh_t3d_reflect(3,.1d0*mesh%hmin,ref_per(i))
! end do
DEBUG_STACK_POP
end subroutine native_load_mesh
!------------------------------------------------------------------------------
!> Read in t3d mesh file from file "filename"
!! - Read in T3D options from input file
!! - Read in mesh points and cells
!! - Read in surface IDs for CAD edges and faces
!------------------------------------------------------------------------------
subroutine native_load_smesh
logical :: is_2d,success
integer(i4) :: i,id,lenreflag,ierr,io_unit,ndims,np_mem
integer(i4), allocatable, dimension(:) :: dim_sizes
real(r8), allocatable, dimension(:,:) :: rtmp
!---Read in mesh options
namelist/native_mesh_options/filename!,inpname,reflect,ref_per,zstretch
DEBUG_STACK_PUSH
!---
np_mem=-1
IF(oft_env%head_proc)THEN
    IF(ALLOCATED(r_mem).AND.ALLOCATED(lc_mem).AND.ALLOCATED(reg_mem))THEN
        np_mem=SIZE(r_mem,DIM=2)
    ELSE
        OPEN(NEWUNIT=io_unit,FILE=oft_env%ifile)
        READ(io_unit,native_mesh_options,IOSTAT=ierr)
        CLOSE(io_unit)
        IF(ierr<0)CALL oft_abort('No "native_mesh_options" found in input file.','native_load_smesh',__FILE__)
        IF(ierr>0)CALL oft_abort('Error parsing "native_mesh_options" in input file.','native_load_smesh',__FILE__)
        IF(TRIM(filename)=='none')CALL oft_abort('No mesh file specified','native_load_smesh',__FILE__)
        ! IF(TRIM(inpname)=='none')CALL oft_abort('No T3D input file specified','native_load_smesh',__FILE__)
        ! lenreflag=lnblnk(reflect)
        !
        WRITE(*,*)
        WRITE(*,'(A)')'**** Loading OFT mesh'
        WRITE(*,'(2X,2A)')'Mesh File = ',TRIM(filename)
        ! WRITE(*,'(2X,2A)')'Geom File = ',TRIM(inpname)
        IF(lenreflag>0)THEN
            ! WRITE(*,'(2X,A)')'Reflection:'
            ! DO i=1,lenreflag
            !     SELECT CASE(reflect(i:i))
            !     CASE('x')
            !         WRITE(*,'(4X,A,L)')'YZ-plane, periodic = ',ref_per(1)
            !     CASE('y')
            !         WRITE(*,'(4X,A,L)')'XZ-plane, periodic = ',ref_per(2)
            !     CASE('z')
            !         WRITE(*,'(4X,A,L)')'XY-plane, periodic = ',ref_per(3)
            !     END SELECT
            ! END DO
        END IF
    END IF
END IF
!---Broadcast input information
#ifdef HAVE_MPI
CALL MPI_Bcast(np_mem,1,OFT_MPI_I4,0,oft_env%COMM,ierr)
IF(ierr/=0)CALL oft_abort('Error in MPI_Bcast','mesh_t3d_load',__FILE__)
CALL MPI_Bcast(filename,80,OFT_MPI_CHAR,0,oft_env%COMM,ierr)
IF(ierr/=0)CALL oft_abort('Error in MPI_Bcast','mesh_t3d_load',__FILE__)
! CALL MPI_Bcast(inpname, 40,OFT_MPI_CHAR,0,oft_env%COMM,ierr)
! IF(ierr/=0)CALL oft_abort('Error in MPI_Bcast','mesh_t3d_load',__FILE__)
! CALL MPI_Bcast(reflect,  3,OFT_MPI_CHAR,0,oft_env%COMM,ierr)
! IF(ierr/=0)CALL oft_abort('Error in MPI_Bcast','mesh_t3d_load',__FILE__)
! CALL MPI_Bcast(ref_per,  3,OFT_MPI_LOGICAL,0,oft_env%COMM,ierr)
! IF(ierr/=0)CALL oft_abort('Error in MPI_Bcast','mesh_t3d_load',__FILE__)
! CALL MPI_Bcast(zstretch, 1,OFT_MPI_R8,0,oft_env%COMM,ierr)
! IF(ierr/=0)CALL oft_abort('Error in MPI_Bcast','mesh_t3d_load',__FILE__)
#endif
!---Read in mesh sizes
IF(np_mem>0)THEN
    ALLOCATE(dim_sizes(2))
    dim_sizes=SHAPE(lc_mem)
ELSE
    CALL hdf5_field_get_sizes(TRIM(filename),"mesh/LC",ndims,dim_sizes)
    IF(ndims==-1)CALL oft_abort('"mesh/LC" field does not exist in input file', 'native_load_smesh', __FILE__)
END IF
IF(dim_sizes(2)==0)CALL oft_abort('Zero length cell list','native_load_smesh',__FILE__)
!---Allocate mesh
SELECT CASE(dim_sizes(1))
    CASE(3)
        ALLOCATE(oft_trimesh::mg_mesh%smeshes(mg_mesh%mgdim))
    CASE(4)
        ALLOCATE(oft_quadmesh::mg_mesh%smeshes(mg_mesh%mgdim))
    CASE DEFAULT
        CALL oft_abort('Invalid cell type','native_load_smesh',__FILE__)
END SELECT
DO i=1,mg_mesh%mgdim
    CALL mg_mesh%smeshes(i)%setup(mesh_native_id,.FALSE.)
END DO
smesh=>mg_mesh%smeshes(1)
smesh%nc=dim_sizes(2)
!
DEALLOCATE(dim_sizes)
IF(np_mem>0)THEN
    ALLOCATE(dim_sizes(2))
    dim_sizes=SHAPE(r_mem)
ELSE
    CALL hdf5_field_get_sizes(TRIM(filename),"mesh/R",ndims,dim_sizes)
    IF(ndims==-1)CALL oft_abort('"mesh/R" field does not exist in input file', 'native_load_smesh', __FILE__)
END IF
is_2d=(dim_sizes(1)==2)
smesh%np=dim_sizes(2)
IF(smesh%np==0)CALL oft_abort('Zero length point list','native_load_smesh',__FILE__)
DEALLOCATE(dim_sizes)
IF(.NOT.oft_env%head_proc)THEN
    DEBUG_STACK_POP
    RETURN
END IF
!---Read in points
ALLOCATE(smesh%r(3,smesh%np))
IF(is_2d)THEN
    smesh%dim=2
    smesh%r=0.d0
    IF(np_mem>0)THEN
        smesh%r(1:2,:)=r_mem
        DEALLOCATE(r_mem)
    ELSE
        ALLOCATE(rtmp(2,smesh%np))
        CALL hdf5_read(rtmp,TRIM(filename),"mesh/R",success)
        IF(.NOT.success)CALL oft_abort('Error reading points','native_load_smesh',__FILE__)
        smesh%r(1:2,:)=rtmp
        DEALLOCATE(rtmp)
    END IF
ELSE
    IF(np_mem>0)THEN
        smesh%r=r_mem
        DEALLOCATE(r_mem)
    ELSE
        CALL hdf5_read(smesh%r,TRIM(filename),"mesh/R",success)
        IF(.NOT.success)CALL oft_abort('Error reading points','native_load_smesh',__FILE__)
    END IF
END IF
!---Read in cells
ALLOCATE(smesh%lc(smesh%cell_np,smesh%nc))
IF(np_mem>0)THEN
    smesh%lc=lc_mem
    DEALLOCATE(lc_mem)
ELSE
    CALL hdf5_read(smesh%lc,TRIM(filename),"mesh/LC",success)
    IF(.NOT.success)CALL oft_abort('Error reading cells','native_load_smesh',__FILE__)
END IF
!---Read in region list
ALLOCATE(smesh%reg(smesh%nc))
IF(np_mem>0)THEN
    smesh%reg=reg_mem
    DEALLOCATE(reg_mem)
ELSE
    CALL hdf5_read(smesh%reg,TRIM(filename),"mesh/REG",success)
    IF(.NOT.success)CALL oft_abort('Error reading region list','native_load_smesh',__FILE__)
END IF
!---Read high-order mesh information (Tetrahedra only)
IF(hdf5_field_exist(TRIM(filename),"mesh/ho_info/LE"))THEN
    mesh_order=2
    CALL hdf5_field_get_sizes(TRIM(filename),"mesh/ho_info/R",ndims,dim_sizes)
    IF(ndims==-1)CALL oft_abort('"mesh/ho_info/R" field does not exist in input file', 'native_load_smesh', __FILE__)
    np_ho=dim_sizes(2)
    ALLOCATE(r_ho(3,np_ho))
    CALL hdf5_read(r_ho,TRIM(filename),"mesh/ho_info/R",success)
    IF(.NOT.success)CALL oft_abort('Error reading quadratic points','native_load_smesh',__FILE__)
    ALLOCATE(le_ho(2,np_ho))
    CALL hdf5_read(le_ho,TRIM(filename),"mesh/ho_info/LE",success)
    IF(.NOT.success)CALL oft_abort('Error reading quadratic edges','native_load_smesh',__FILE__)
ELSE
    mesh_order=1
END IF
IF(oft_debug_print(2))WRITE(*,*)'  Complete'
!---
! call mesh_t3d_geom
call mesh_global_resolution(smesh)
! !---
! do i=1,lenreflag
!     if(reflect(i:i)=='x')call mesh_t3d_reflect(1,.1d0*mesh%hmin,ref_per(i))
!     if(reflect(i:i)=='y')call mesh_t3d_reflect(2,.1d0*mesh%hmin,ref_per(i))
!     if(reflect(i:i)=='z')call mesh_t3d_reflect(3,.1d0*mesh%hmin,ref_per(i))
! end do
DEBUG_STACK_POP
end subroutine native_load_smesh
!------------------------------------------------------------------------------
!> Needs Docs
!------------------------------------------------------------------------------
subroutine native_read_nodesets(nsets,native_filename)
TYPE(oft_1d_int), pointer, intent(inout) :: nsets(:)
CHARACTER(LEN=80), OPTIONAL, INTENT(in) :: native_filename
INTEGER(4) :: j,num_nsets,ndims
INTEGER(4), ALLOCATABLE, DIMENSION(:) :: dim_sizes
LOGICAL :: success
CHARACTER(LEN=4) :: blknum
CHARACTER(LEN=80) :: local_filename
!---Open mesh file
local_filename=filename
IF(PRESENT(native_filename))local_filename=native_filename
! Look for sideset field
IF(.NOT.hdf5_field_exist(local_filename,"mesh/NUM_NODESETS"))RETURN
!---Read nodesets
CALL hdf5_read(num_nsets,local_filename,"mesh/NUM_NODESETS",success=success)
IF(.NOT.success)RETURN
ALLOCATE(nsets(num_nsets))
DO j=1,num_nsets
  WRITE(blknum,'(I4.4)')j
  CALL hdf5_field_get_sizes(local_filename,"mesh/NODESET"//blknum,ndims,dim_sizes)
  nsets(j)%n=dim_sizes(1)
  DEALLOCATE(dim_sizes)
  ALLOCATE(nsets(j)%v(nsets(j)%n))
  CALL hdf5_read(nsets(j)%v,local_filename,"mesh/NODESET"//blknum,success=success)
  IF(.NOT.success)nsets(j)%n=-1
END DO
CALL oft_decrease_indent
END SUBROUTINE native_read_nodesets
!------------------------------------------------------------------------------
!> Needs Docs
!------------------------------------------------------------------------------
subroutine native_read_sidesets(ssets,native_filename)
TYPE(oft_1d_int), pointer, intent(inout) :: ssets(:)
CHARACTER(LEN=80), OPTIONAL, INTENT(in) :: native_filename
INTEGER(4) :: j,num_ssets,ndims
INTEGER(4), ALLOCATABLE, DIMENSION(:) :: dim_sizes
LOGICAL :: success
CHARACTER(LEN=4) :: blknum
CHARACTER(LEN=80) :: local_filename
!---Open mesh file
local_filename=filename
IF(PRESENT(native_filename))local_filename=native_filename
! Look for sideset field
IF(.NOT.hdf5_field_exist(local_filename,"mesh/NUM_SIDESETS"))RETURN
!---Read nodesets
CALL hdf5_read(num_ssets,local_filename,"mesh/NUM_SIDESETS",success=success)
IF(.NOT.success)RETURN
ALLOCATE(ssets(num_ssets))
DO j=1,num_ssets
    WRITE(blknum,'(I4.4)')j
    CALL hdf5_field_get_sizes(local_filename,"mesh/SIDESET"//blknum,ndims,dim_sizes)
    ssets(j)%n=dim_sizes(1)
    DEALLOCATE(dim_sizes)
    ALLOCATE(ssets(j)%v(ssets(j)%n))
    CALL hdf5_read(ssets(j)%v,local_filename,"mesh/SIDESET"//blknum,success=success)
    IF(.NOT.success)ssets(j)%n=-1
END DO
CALL oft_decrease_indent
END SUBROUTINE native_read_sidesets
!------------------------------------------------------------------------------
!> Add quadratic mesh node points from high order import
!------------------------------------------------------------------------------
subroutine native_hobase(self)
CLASS(oft_amesh), INTENT(inout) :: self
DEBUG_STACK_PUSH
IF(np_ho==0)RETURN
if(oft_debug_print(1))write(*,'(2A)')oft_indent,'Importing quadratic mesh nodes'
SELECT CASE(self%type)
CASE(1) ! Tet/Tri
    CALL native_hobase_simplex(self)
CASE(3) ! Hex/Quad
  CALL oft_abort("High-order import not supported for native interface and Hex/Quad meshes", &
    "native_hobase", __FILE__)
!   SELECT TYPE(self)
!   CLASS IS(oft_bmesh)
!     CALL native_hobase_quad(self)
!   CLASS IS(oft_mesh)
!     CALL native_hobase_hex(self)
!   END SELECT
CASE DEFAULT
  CALL oft_abort("Unknown element type", "native_hobase", __FILE__)
END SELECT
DEBUG_STACK_POP
end subroutine native_hobase
!------------------------------------------------------------------------------
!> Add quadratic mesh node points to a simplex mesh (Tet/Tri) from high order import
!------------------------------------------------------------------------------
subroutine native_hobase_simplex(self)
CLASS(oft_amesh), INTENT(inout) :: self
real(r8) :: pt(3)
integer(i4) :: i,j,k,etmp(2)
DEBUG_STACK_PUSH
!---Setup quadratic mesh
self%order=1
self%ho_info%nep=1
ALLOCATE(self%ho_info%r(3,self%ne),self%ho_info%lep(self%ho_info%nep,self%ne))
!---Initialize high order points with straight edges
DO i=1,self%ne
    self%ho_info%lep(1,i)=i
    self%ho_info%r(:,i)=(self%r(:,self%le(1,i))+self%r(:,self%le(2,i)))/2.d0
END DO
!---Set midpoints from imported list
DO i=1,np_ho
    etmp=le_ho(:,i)
    k=ABS(mesh_local_findedge(self,etmp))
    if(k==0)CALL oft_abort('Unlinked mesh edge','native_hobase',__FILE__)
    self%ho_info%r(:,k) = r_ho(:,i)
END DO
!---Destory temporary storage
DEALLOCATE(r_ho,le_ho)
DEBUG_STACK_POP
end subroutine native_hobase_simplex
end module oft_mesh_native